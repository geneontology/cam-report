PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX enabled_by: <http://purl.obolibrary.org/obo/RO_0002333>
PREFIX occurs_in: <http://purl.obolibrary.org/obo/BFO_0000066>
PREFIX part_of: <http://purl.obolibrary.org/obo/BFO_0000050>
PREFIX metago: <http://geneontology.org/>
PREFIX ECO: <http://purl.obolibrary.org/obo/ECO_0000000>
PREFIX evidence: <http://geneontology.org/lego/evidence>

SELECT ?cam ?m ?relation ?o
WHERE {

  VALUES ?relation { enabled_by: occurs_in: part_of: }

  GRAPH ?cam {
    ?cam metago:graphType metago:noctuaCam .

    ?m ?relation ?o .

    FILTER NOT EXISTS {
      ?b owl:annotatedSource ?m .
      ?b owl:annotatedProperty ?relation .
      ?b owl:annotatedTarget ?o .

      ?b evidence: ?e .
      ?e a ?evidenceType .

      ?evidenceType rdfs:subClassOf* ECO: .
    }
  }
}

LIMIT 2000
