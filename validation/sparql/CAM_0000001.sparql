PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX MF: <http://purl.obolibrary.org/obo/GO_0003674>
PREFIX CC: <http://purl.obolibrary.org/obo/GO_0005575>
PREFIX BP: <http://purl.obolibrary.org/obo/GO_0008150>
PREFIX enabled_by: <http://purl.obolibrary.org/obo/RO_0002333>
PREFIX occurs_in: <http://purl.obolibrary.org/obo/BFO_0000066>
PREFIX part_of: <http://purl.obolibrary.org/obo/BFO_0000050>
PREFIX metago: <http://geneontology.org/>
PREFIX has_evidence: <http://geneontology.org/lego/evidence>

SELECT ?cam ?mf ?mfClass
WHERE {

  VALUES ?relation { enabled_by: occurs_in: part_of: }

  GRAPH ?cam {
    ?cam metago:graphType metago:noctuaCam .

    ?mf ?relation ?o .
    ?mf a ?mfClass .

    FILTER (?mfClass != <http://www.w3.org/2002/07/owl#NamedIndividual>)
  }

  FILTER NOT EXISTS {
    ?mfClass rdfs:subClassOf* MF: .
  }
}
