PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX MF: <http://purl.obolibrary.org/obo/GO_0003674>
PREFIX CP: <http://purl.obolibrary.org/obo/GO_0005575>
PREFIX BP: <http://purl.obolibrary.org/obo/GO_0008150>
PREFIX enabled_by: <http://purl.obolibrary.org/obo/RO_0002333>
PREFIX enables: <http://purl.obolibrary.org/obo/RO_0002327>
PREFIX occurs_in: <http://purl.obolibrary.org/obo/BFO_0000066>
PREFIX part_of: <http://purl.obolibrary.org/obo/BFO_0000050>
PREFIX metago: <http://geneontology.org/>
PREFIX kinase_activity: <http://purl.obolibrary.org/obo/GO_0016301>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX evidence: <http://geneontology.org/lego/evidence>
PREFIX has_supporting_evidence: <http://purl.obolibrary.org/obo/SEPIO_0000124>
PREFIX dc: <http://purl.org/dc/elements/1.1/>

SELECT (COUNT(DISTINCT ?c) as ?papers) ?v
WHERE {
  GRAPH ?cam {
    ?cam metago:graphType metago:noctuaCam .

    ?b owl:annotatedSource ?a ;
       owl:annotatedProperty ?r ;
       owl:annotatedTarget ?o .

    ?a ?r ?o .

    ?b dc:contributor ?v .

    ?b evidence: ?e .
    ?e a ?etype .
    ?e has_supporting_evidence: ?c .
  }

  ?etype rdfs:subClassOf* <http://purl.obolibrary.org/obo/ECO_0000000> .
}
GROUP BY ?v
