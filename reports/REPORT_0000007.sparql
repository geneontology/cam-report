PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX enabled_by: <http://purl.obolibrary.org/obo/RO_0002333>
PREFIX occurs_in: <http://purl.obolibrary.org/obo/BFO_0000066>
PREFIX part_of: <http://purl.obolibrary.org/obo/BFO_0000050>
PREFIX : <http://geneontology.org/>

CONSTRUCT {
  ?inconsistent :definedIn ?cam .
  ?inconsistent ?outgoing ?objects .
  ?subjects ?incoming ?inconsistent .
}
WHERE {
  GRAPH ?cam {
    ?cam :graphType :noctuaCam .
    ?cam <http://geneontology.org/lego/modelstate> "production"^^xsd:string .

    ?inconsistent a ?asserted .
    ?inconsistent ?outgoing ?objects .
    ?subjects ?incoming ?inconsistent .
  }

  GRAPH ?cam_inferred {
    ?cam_inferred :inferredFrom ?cam .

    ?inconsistent a owl:Nothing .
  }
  FILTER (?asserted != <http://www.w3.org/2002/07/owl#NamedIndividual>)
}
